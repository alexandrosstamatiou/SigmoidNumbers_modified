#a few patches that make the builtin lu factorization possible.

#taken from base/LinAlg/generic.jl

if VERSION <= v"0.6.0"
    #apparently these issues have been fixed in MASTER past v0.6.0

function Base.LinAlg.istril(A::AbstractMatrix)
    m, n = size(A)
    for j = 2:n, i = 1:min(j-1,m)
        if A[i,j] != zero(eltype(A))
            return false
        end
    end
    return true
end

function Base.LinAlg.istriu(A::AbstractMatrix)
    m, n = size(A)
    for j = 1:min(n,m-1), i = j+1:m
        if A[i,j] != zero(eltype(A))
            return false
        end
    end
    return true
end

#taken from base/LinAlg/lu.jl
function Base.LinAlg.generic_lufact!(A::StridedMatrix{T}, ::Type{Val{Pivot}} = Val{true}) where {T,Pivot}
    m, n = size(A)
    minmn = min(m,n)
    info = 0
    ipiv = Vector{Base.LinAlg.BlasInt}(minmn)
    @inbounds begin
        for k = 1:minmn
            # find index max
            kp = k
            if Pivot
                amax = real(zero(T))
                for i = k:m
                    absi = abs(A[i,k])
                    if absi > amax
                        kp = i
                        amax = absi
                    end
                end
            end
            ipiv[k] = kp
            if A[kp,k] != zero(T)
                if k != kp
                    # Interchange
                    for i = 1:n
                        tmp = A[k,i]
                        A[k,i] = A[kp,i]
                        A[kp,i] = tmp
                    end
                end
                # Scale first column
                Akkinv = inv(A[k,k])
                for i = k+1:m
                    A[i,k] *= Akkinv
                end
            elseif info == 0
                info = k
            end
            # Update the rest
            for j = k+1:n
                for i = k+1:m
                    A[i,j] -= A[i,k]*A[k,j]
                end
            end
        end
    end
    Base.LinAlg.LU{T,typeof(A)}(A, ipiv, convert(Base.LinAlg.BlasInt, info))
end

end
